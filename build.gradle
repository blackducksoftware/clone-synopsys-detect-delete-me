buildscript {
    ext {
        springBootVersion = '2.0.3.RELEASE'
    }
    repositories {
        jcenter()
        mavenCentral()
        maven { url 'https://plugins.gradle.org/m2/' }
    }

    dependencies {
        classpath 'com.blackducksoftware.integration:common-gradle-plugin:0.0.49'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}"
    }
}

version = '5.3.0-SNAPSHOT'
apply plugin: 'com.blackducksoftware.integration.solution'
apply plugin: 'org.springframework.boot'
apply from: 'airgap.gradle'
apply from: 'artifactory-properties.gradle'

allprojects {
    apply plugin: 'groovy'
    apply plugin: 'maven-publish'
    apply plugin: 'io.spring.dependency-management'

    dependencies {
        implementation 'com.blackducksoftware.integration:blackduck-common:42.1.0'
        implementation 'com.paypal.digraph:digraph-parser:1.0'
    }
}

subprojects {
    version = rootProject.version
    apply plugin: 'com.blackducksoftware.integration.simple'

    license {
        header rootProject.file('LICENSE')
    }

    dependencyManagement {
        imports {
            mavenBom("org.springframework.boot:spring-boot-dependencies:${springBootVersion}")
        }
    }
}

build {
    doLast {
        def shellScriptTemplateFile = new File("${projectDir}/src/main/resources/detect-sh")
        def shellScriptContents = shellScriptTemplateFile.getText('UTF-8')
        def shellScriptFile = new File("${buildDir}/detect.sh")
        shellScriptFile.delete()
        shellScriptFile << shellScriptContents
        shellScriptFile.setExecutable(true)

        def powershellScriptTemplateFile = new File("${projectDir}/src/main/resources/detect-ps")
        def powershellScriptContents = powershellScriptTemplateFile.getText('UTF-8')
        def powershellScriptFile = new File("${buildDir}/detect.ps1")
        powershellScriptFile.delete()
        powershellScriptFile << powershellScriptContents
        powershellScriptFile.setExecutable(true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            def jarName = "${buildDir}/libs/${project.name}-${version}.jar"
            artifact(jarName)
        }
    }
}

artifactory {
    publish {
        defaults { publications('mavenJava') }
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

dependencies {
    implementation project(":detect-configuration")

    implementation 'com.synopsys.integration:polaris-common:0.7.1'

    implementation 'org.apache.maven.shared:maven-invoker:3.0.0'
    implementation 'com.esotericsoftware.yamlbeans:yamlbeans:1.11'
    implementation 'com.moandjiezana.toml:toml4j:0.7.1'
    implementation 'org.codehaus.groovy:groovy-all:2.4.12'
    implementation 'org.freemarker:freemarker:2.3.26-incubating'
    implementation 'org.springframework.boot:spring-boot-starter'

    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.5'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.5'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.5'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

springBoot { mainClassName = 'com.synopsys.integration.detect.Application' }

if ("true" == project.findProperty('refresh.cache')) {
    project.dependencies {
        implementation 'com.blackducksoftware.integration:integration-gradle-inspector:0.6.0'
    }

    project.repositories.clear()
    project.repositories {
        maven {
            url 'https://repo.blackducksoftware.com:443/artifactory/bds-integration-public-cache'
            credentials {
                username = project.ext.artifactoryDeployerUsername
                password = project.ext.artifactoryDeployerPassword
            }
        }
    }
}
