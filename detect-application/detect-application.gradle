def artifactName = "synopsys-detect";
apply plugin: 'org.springframework.boot'

apply from: 'airgap.gradle'
apply from: 'artifactory-properties.gradle'

project.ext.archivesBaseName = "synopsys-detect"
archivesBaseName = "synopsys-detect"

build {
    doLast {
        def shellScriptTemplateFile = new File("${projectDir}/src/main/resources/detect-sh")
        def shellScriptContents = shellScriptTemplateFile.getText('UTF-8')
        def shellScriptFile = new File("${buildDir}/detect.sh")
        shellScriptFile.delete()
        shellScriptFile << shellScriptContents
        shellScriptFile.setExecutable(true)

        def powershellScriptTemplateFile = new File("${projectDir}/src/main/resources/detect-ps")
        def powershellScriptContents = powershellScriptTemplateFile.getText('UTF-8')
        def powershellScriptFile = new File("${buildDir}/detect.ps1")
        powershellScriptFile.delete()
        powershellScriptFile << powershellScriptContents
        powershellScriptFile.setExecutable(true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            def jarName = "${buildDir}/libs/${artifactName}-${version}.jar"
            artifact(jarName)
        }
    }
}

artifactory {
    publish {
        defaults { publications('mavenJava') }
    }
}

test {
    testLogging {
        exceptionFormat = 'full'
    }
}

dependencies {
    implementation project(":detect-configuration")

    implementation 'com.synopsys.integration:polaris-common:0.4.0'

    implementation 'org.apache.maven.shared:maven-invoker:3.0.0'
    implementation 'com.esotericsoftware.yamlbeans:yamlbeans:1.11'
    implementation 'com.moandjiezana.toml:toml4j:0.7.1'
    implementation 'org.codehaus.groovy:groovy-all:2.4.12'
    implementation 'org.freemarker:freemarker:2.3.26-incubating'
    implementation 'org.springframework.boot:spring-boot-starter'

    implementation 'com.fasterxml.jackson.dataformat:jackson-dataformat-yaml:2.9.5'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.9.5'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.9.5'

    testImplementation 'org.springframework.boot:spring-boot-starter-test'
}

springBoot { mainClassName = 'com.blackducksoftware.integration.detect.Application' }

if ("true" == project.findProperty('refresh.cache')) {
    project.dependencies {
        implementation 'com.blackducksoftware.integration:integration-gradle-inspector:0.6.0'
    }

    project.repositories.clear()
    project.repositories {
        maven {
            url 'https://repo.blackducksoftware.com:443/artifactory/bds-integration-public-cache'
            credentials {
                username = project.ext.artifactoryDeployerUsername
                password = project.ext.artifactoryDeployerPassword
            }
        }
    }
}
